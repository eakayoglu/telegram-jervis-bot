'------------------------------------------------------------------------------
 Function: PersonelBul
' Description: Searches for a personnel object in M-Files based on specific conditions.
' Parameters:
'   - MFilesUA: The M-Files user account identifier.
' Returns:
'   - The ID of the first personnel object that matches the search conditions.
' 
' The function creates search conditions to find a personnel object by:
'   1. Class property (using a predefined personnel class ID).
'   2. M-Files user account ID.
'   3. Ensuring the object is not marked as deleted.
' 
' The search results are then used to return the ID of the first matching personnel object.

' Written by: Emre Akayoglu
'------------------------------------------------------------------------------

Function PersonelBul(MFilesUA)
	Dim oScP: Set oScp = CreateObject("MFilesAPI.SearchCondition")
	Dim oScsP: Set OscsP = CreateObject("MFilesAPI.SearchConditions")
	'Class
	oScP.Expression.DataPropertyValuePropertyDef = MFBuiltInPropertyDefClass
	oScP.ConditionType = MFConditionTypeEqual
	oScP.TypedValue.SetValue MFDatatypeLookup, cPersonelID
	oScsP.Add -1, oScP
	'MFilesUser
	oScP.Expression.DataPropertyValuePropertyDef = pMFilesUserID
	oScP.ConditionType = MFConditionTypeEqual
	oScP.TypedValue.SetValue MFDataTypeLookup, MFilesUA
	oScsP.Add -1, oScP														
	'Deleted
	oScP.Expression.DataStatusValueType = MFStatusTypeDeleted 
	oScP.ConditionType = MFConditionTypeEqual
	oScP.TypedValue.SetValue MFDatatypeBoolean, false
	oScsP.Add -1, oScP
	Dim oSearchResultsP : Set oSearchResultsP = Vault.ObjectSearchOperations.SearchForObjectsByConditions(oScsP,MFSearchFlagNone,False)
	PersonelBul = oSearchResultsP.Item(1).ObjVer.ObjId.ID
End Function