'------------------------------------------------------------------------------
' Function: upperBakalim
' Description: This function converts specific Turkish characters in a property value to their uppercase equivalents and updates the property value in the Vault.
' Parameters:
'   iAlias - The alias of the property definition.
' Variables:
'   pPropID - The ID of the property definition.
'   pPropIDDef - The data type of the property definition.
'   prp - The display value of the property.
'   oPropertyValue - The property value object to be updated.
' Notes:
'   - The function checks if the property exists and is not empty.
'   - It replaces specific Turkish characters with their uppercase equivalents.
'   - It updates the property value in the Vault based on the data type (text or multi-line text).
'   - The function uses M-Files API objects and methods.

' Written by: Emre Akayoglu
'------------------------------------------------------------------------------

Function upperBakalim(iAlias)
	Dim pPropID : pPropID = Vault.PropertyDefOperations.GetPropertyDefIDByAlias(iAlias)
	Dim pPropIDDef : pPropIDDef = Vault.PropertyDefOperations.GetPropertyDef(pPropID).DataType

	IF PropertyValues.IndexOf(pPropID) <> -1 THEN
		Dim prp : prp = PropertyValues.SearchForProperty(pPropID).TypedValue.DisplayValue
		IF prp <> "" Then
			prp = Replace(prp,"i","İ")
			prp = Replace(prp,"ı","I")
			prp = Replace(prp,"ö","Ö")
			prp = Replace(prp,"ü","Ü")
			prp = Replace(prp,"ç","Ç")
			prp = Replace(prp,"ğ","Ğ")
			prp = Replace(prp,"ş","Ş")
			prp = Ucase(prp)

			Dim oPropertyValue : Set oPropertyValue = CreateObject("MFilesAPI.PropertyValue")
			oPropertyValue.PropertyDef = pPropID

			If pPropIDDef = 1 Then ' MFDataTypeText
				oPropertyValue.TypedValue.SetValue MFDataTypeText, prp
			elseif pPropIDDef = 13 Then 'MFDatatypeMultiLineText
				oPropertyValue.TypedValue.SetValue MFDatatypeMultiLineText, prp
			End If

			Vault.ObjectPropertyOperations.SetProperty ObjVer, oPropertyValue
		End IF
	END IF
End Function